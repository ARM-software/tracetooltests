#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_buffer_reference : require

layout(local_size_x_id = 0) in;
layout(local_size_y_id = 1) in;
layout(local_size_z_id = 2) in;
layout(constant_id = 3) const int WIDTH = 640;
layout(constant_id = 4) const int HEIGHT = 480;
layout(constant_id = 5) const uint numPixelsPerBuffer = 307200;

struct InterleaveData
{
	uvec2 color;
	uvec2 address;
};

layout(buffer_reference, std430, buffer_reference_align=8) readonly buffer ColorBufferRef
{
	uvec2 colors[];
};

layout(buffer_reference, std430, buffer_reference_align=8) readonly buffer AddressBufferRef
{
	uvec2 addresses[];
};

// interleaved data with color value and address value
layout(buffer_reference, std430, buffer_reference_align=8) writeonly buffer InterleaveBuffer
{
	InterleaveData interleave[];
};

layout(std430, push_constant) uniform PushConstants
{
	InterleaveBuffer interleaveBDA;
	ColorBufferRef colorBDA;
	AddressBufferRef addressBDA;
} pushconstants;

void main()
{
	// In order to fit the work into workgroups, some unnecessary threads are launched.
	// We terminate those threads here. 
	if (gl_GlobalInvocationID.x >= WIDTH || gl_GlobalInvocationID.y >= HEIGHT) return;

	uint pixelIndex = gl_GlobalInvocationID.y * WIDTH + gl_GlobalInvocationID.x;

	uvec2 color = pushconstants.colorBDA.colors[pixelIndex];
	uvec2 address = pushconstants.addressBDA.addresses[pixelIndex];

	pushconstants.interleaveBDA.interleave[pixelIndex].color = color;
	pushconstants.interleaveBDA.interleave[pixelIndex].address = address;
}
